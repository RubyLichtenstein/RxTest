group 'com.rubylichtenstein'
version '1.0.1'


buildscript {
    ext.kotlin_version = '1.2.0'
    ext.detekt_version = '1.0.0.RC6'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: "jacoco"
apply plugin: "io.gitlab.arturbosch.detekt"

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

dependencies {
    compile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'io.reactivex.rxjava2:rxjava:2.1.7'
 }

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

detekt {
    version = "$detekt_version"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/rxkotlintest-" + version + ".pom")
    }
}
